resource "aws_iam_user" "github" {
  name = var.github_iam_username

  tags = {
    Name = "GitHub Actions deploy user for site"
  }
}

resource "aws_iam_user_policy" "github_site_deploy" {
  name = "github-site-deploy-policy"
  user = aws_iam_user.github.name

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "AllowS3SiteBucketAccess"
        Effect = "Allow"
        Action = [
          "s3:PutObject",
          "s3:PutObjectAcl",
          "s3:DeleteObject",
          "s3:ListBucket"
        ]
        Resource = [
          aws_s3_bucket.site.arn,
          "${aws_s3_bucket.site.arn}/*"
        ]
      },
      {
        Sid    = "AllowCloudFrontInvalidation"
        Effect = "Allow"
        Action = [
          "cloudfront:CreateInvalidation",
          "cloudfront:GetInvalidation",
          "cloudfront:ListInvalidations"
        ]
        Resource = [
          aws_cloudfront_distribution.site.arn
        ]
      }
    ]
  })
}

resource "aws_iam_access_key" "github" {
  user = aws_iam_user.github.name
}
